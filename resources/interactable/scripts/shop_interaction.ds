


if (<.closed>) {
    if (<.closed_message>) {
        output(
            format(<.closed_message>, shop_name=<.shop_name>)
        )
    }
} else {
    if (<.enter_message>) {
        output(
            format(<.enter_message>, shop_name=<.shop_name>, shopkeeper=<.shopkeeper>)
        )
    }
    
    // restock if needed
    if ([engine:time/check](<.restock_time>) or (not <.initial_stock>)) {
        <.initial_stock> = true
        <.current_stock> = [engine:interaction/get_shop_stock](<.stock>)
    }
    if (length(<.current_stock>) == 0) {
        if (<.out_of_stock_message>) {
            output(
                format(<.out_of_stock_message>, shop_name=<.shop_name>, shopkeeper=<.shopkeeper>)
            )
        }
        return
    }

    // display shop contents

    // [engine:log/debug](<w>)
    /*
    ┌─ The Armory ──────────────────────────────────────────────────
    │          Lightning Arrow   x4 : 2 Gold  5 Copper
    │               Fire Arrow  x16 : 1 Gold
    │               Long Sword      : 1 Gold  5 Silver
    │                    Stick  x12 : 5 Copper
    ├─ Weston ──────────────────────────────────────────────────────
    │ 10 Gold  5 Silver  5 Copper
    */

    $shop_display = {
        <output> = format("```less\n┌─ {shop_name} {c:─<{w}}", shop_name=<.shop_name>, c="─", w=max(1, (40-length(<.shop_name>))))
        for <item> in <.current_stock> {
            //         `Sword`
            <output> = join(
                <output>,
                format(
                    "│ {name: >18} {amount: >4} : {cost}",
                    name=join("`", <item>["name"], "`", seperator=""),
                    amount=<item>["amount"],
                    cost=[engine:text/convert](<item>["cost"])
                ),
                seperator="\n"
            )
        }
        <output> = join(
            <output>,
            format("├─ {player} {c:─<{w}}", player=<#player.name>, c="─", w=max(1, 40-length(<#player.name>))),
            format("│ {money}", money=<#player.currency>),
            "│ say `exit` to leave the shop\n```",
            seperator="\n"
        )
        output(<output>)
    }

    $shop_display

    // check inputs

    while (true) {
        <inp> = input()


        [engine:text/match](<inp>)
        @pattern: "\b(exit|leave)\b" # {
            if (<.exit_message>) {
                output(
                    format(<.exit_message>, shop_name=<.shop_name>, shopkeeper=<.shopkeeper>)
                )
            }
            break
        }

        for <item> in <.current_stock> {
            [engine:text/match]([engine:text/set_case](<inp>, "lower"), "fullmatch")
            @pattern: [engine:text/set_case](<item>["name"], "lower") # {

                if (<item>["cost"] > <#player.currency>) {
                    output(
                        format("You do not have enough money to buy `{item}`", item=<item>["name"])
                    )
                    break
                }

                // output()
                <response> = input(
                    format("Purchase `{item}` for `{cost}`? (yes/no)", item=<item>["name"], cost=<item>["cost"])
                )

                // output("got input!")

                [engine:text/match](<response>)
                @pattern: "[Yy](?:es)?" # {
                    output(
                        format("You purchased `{item}`!", item=<item>["name"])
                    )
                    [engine:player/give_game_object](<item>["item"])
                    [engine:list/remove](<.current_stock>, <item>)

                    $shop_display

                    break

                }

            }
        }

    }

}
