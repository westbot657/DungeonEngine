
if (<.closed>) {
    if (<.closed_message>) {
        output(
            format(<.closed_message>, shop_name=<.shop_name>)
        )
    }
} else {
    if (<.enter_message>) {
        output(
            format(<.enter_message>, shop_name=<.shop_name>, shopkeeper=<.shopkeeper>)
        )
    }

    if (time.check(<.restock_time>) or (not <.initial_stock>)) {
        <.initial_stock> = true
        <.current_stock> = [engine:interaction/get_shop_stock](<.stock>)
    }
    if (length(<.current_stock>) == 0) {
        if (<.out_of_stock_message>) {
            output(
                format(<.out_of_stock_message>, shop_name=<.shop_name>, shopkeeper=<.shopkeeper>)
            )
        }
        return
    }

    $shop_display = {
        <output> = format("```less\n┌─ {shop_name} {c:─<{w}}", shop_name=<.shop_name>, c="─", w=max(1, (40-length(<.shop_name>))))
        for <item> in <.current_stock> {
            <output> = <output>
            ..format(
                "│ {name: >18} {amount: >4} : {cost}",
                name="`"..<item>["name"].."`"::"",
                amount=<item>["amount"],
                cost=[engine:text/convert](<item>["cost"])
            )
            ::"\n"
        }

        <output> = <output>
        ..format("├─ {player} {c:─<{w}}", player=<#player.name>, c="─", w=max(1, 40-length(<#player.name>)))
        ..format("│ {money}", money=<#player.currency>)
        .."│ say `exit` to leave the shop\n```"
        ::"\n"
        output(<output>)
    }

    $shop_display

    while (true) {
        <inp> = input()

        match(<inp>)
        @pattern: "\b(exit|leave)\b" # {
            if (<.exit_message>) {
                output(
                    format(<.exit_message>, shop_name=<.shop_name>, shopkeeper=<.shopkeeper>)
                )
            }
            break
        }

        for <item> in <.current_stock> {
            match([engine:text/set_case](<inp>, "lower"), "fullmatch")
            @pattern: [engine:text/set_case](<item>["name"], "lower") # {

                if (<item>["cost"] > <#player.currency>) {
                    output(
                        format("You do not have enough money to buy `{item}`", item=<item>["name"])
                    )
                    break
                }
                <response> = input(
                    format("Purchase `{item}` for `{cost}`? (yes/no)", item=<item>["name"], cost=<item>["cost"])
                )

                match(<response>)
                @pattern: "[Yy](?:es)?" # {
                    output(
                        format("You purchased `{item}`!", item=<item>["name"])
                    )
                    [engine:player/give_game_object](<item>["item"])
                    [engine:list/remove](<.current_stock>, <item>)

                    $shop_display

                    break
                }
            }
        }
    }
}
